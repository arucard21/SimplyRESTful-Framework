apply plugin: 'java-library'

project.description = "A generic client that can be used with any SimplyRESTful API"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

testing {
	suites { 
    	test { 
            useJUnitJupiter()
        }
        integrationTest(JvmTestSuite) { 
            dependencies {
				implementation project()
			}
			configurations.integrationTestImplementation {
				extendsFrom configurations.testImplementation
			}
            targets { 
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }            
        }
        e2eTest(JvmTestSuite) { 
			configurations.e2eTestImplementation {
				extendsFrom configurations.testImplementation
			}
            targets { 
                all {
                    testTask.configure {
                        shouldRunAfter(test)
                    }
                }
            }            
        }
    }
}

repositories {
	mavenCentral()
}

dependencies {
	api project(":SimplyRESTful-resources")
	api project(":jaxrs-providers")
	api group: "jakarta.json", name: "jakarta.json-api", version: jakartaJsonVersion
	api group: "io.swagger.core.v3", name: "swagger-models-jakarta", version: swaggerVersion
	api group: "io.swagger.parser.v3", name: "swagger-parser", version: swaggerParserVersion
	api group: 'com.fasterxml.jackson.jakarta.rs', name: 'jackson-jakarta-rs-json-provider', version: jacksonVersion

	// dependencies shared by integrationTest and e2eTest
	testImplementation project(":SimplyRESTful")
	testImplementation(platform(group: "org.junit", name: "junit-bom", version: junitVersion))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation group: "org.mockito", name: "mockito-core", version: mockitoVersion
	testImplementation group: "org.mockito", name: "mockito-junit-jupiter", version: mockitoVersion
	testImplementation group: "org.glassfish.jersey.core", name: "jersey-client", version: jerseyVersion
	testImplementation group: "org.glassfish.jersey.inject", name: "jersey-hk2", version: jerseyVersion
	testImplementation group: "org.eclipse.parsson", name: "parsson", version: parssonVersion
	
	integrationTestImplementation group: "org.glassfish.jersey.test-framework", name: "jersey-test-framework-core", version: jerseyVersion
	integrationTestImplementation group: "org.glassfish.jersey.test-framework.providers", name: "jersey-test-framework-provider-jetty", version: jerseyVersion
	integrationTestImplementation group: 'org.glassfish.jersey.media', name: 'jersey-media-sse', version: jerseyVersion
	integrationTestImplementation group: "jakarta.xml.bind", name: "jakarta.xml.bind-api", version: jakartaXmlBindVersion
	integrationTestImplementation group: "io.swagger.core.v3", name: "swagger-jaxrs2-jakarta", version: swaggerVersion
	integrationTestImplementation group: "ch.qos.logback", name: "logback-classic", version: logbackClassicVersion
	
	e2eTestImplementation project(":examples:examples-resources-jpa")
}

publishing{
	publications{
		mavenJava(MavenPublication){
			pom {
				licenses {
					license {
						name = 'GNU Lesser General Public License'
						url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
					}
				}
			}
		}
	}
}