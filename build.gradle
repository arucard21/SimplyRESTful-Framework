plugins{
    id "org.springframework.boot" apply false
    id "com.github.ben-manes.versions"
    id 'jvm-test-suite'
}

//Shared configuration for all projects (root, library and example projects) including ones with a custom build.gradle
configure(allprojects){
    apply plugin: "eclipse"

    group = 'com.github.arucard21.simplyrestful'
    version = '1.1.0'
}

// Publishing configuration for all library projects (excluding root and example projects)
configure(subprojects - project(":examples").allprojects){
    apply plugin: 'java'
	apply plugin: 'maven-publish'
	apply plugin: 'signing'

	java {
    	withJavadocJar()
    	withSourcesJar()
	}

	publishing{
		publications{
			mavenJava(MavenPublication){
				from components.java
				afterEvaluate {
					pom {
						name = project.name
						packaging = 'jar'
						description = project.description
						url = 'https://github.com/arucard21/SimplyRESTful-Framework'

						scm {
							connection = 'scm:git:git://github.com:arucard21/SimplyRESTful-Framework.git'
							developerConnection = 'scm:git:ssh://github.com:arucard21/SimplyRESTful-Framework.git'
							url = 'https://github.com/arucard21/SimplyRESTful-Framework'
						}

						developers {
							developer {
								name = 'Riaas Mokiem'
								email = 'arucard21@gmail.com'
                                organizationUrl = 'https://github.com/arucard21'
							}
						}
					}
				}
			}
		}

		repositories{
			maven{
				def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            	def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            	url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            	credentials {
            		username ossrhUsername
            		password ossrhPassword
        		}
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}
}
